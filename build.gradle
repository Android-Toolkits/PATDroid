apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'signing'

group = 'me.mxia'
archivesBaseName = 'patdroid'
sourceCompatibility = 1.6
version = '1.0.1-SNAPSHOT'
mainClassName = "patdroid.Main"

repositories {
    mavenCentral()
}

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile group: 'org.smali', name: 'dexlib2', version: '2.1.3'
    compile 'com.google.guava:guava:21.0'
}

test {
    systemProperty 'regtest.apkpath', System.getProperty('regtest.apkpath')
    systemProperty 'regtest.updatedump', System.getProperty('regtest.updatedump')
    testLogging.showStandardStreams = true
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
       beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
       if (rootProject.hasProperty('ossrhUsername') && rootProject.hasProperty('ossrhPassword')) {
         repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
           authentication(userName: ossrhUsername, password: ossrhPassword)
         }

         snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
           authentication(userName: ossrhUsername, password: ossrhPassword)
         }
      }
      

      pom.project {
        name 'PATDroid'
        packaging 'jar'
        description 'A Program Analysis Toolkit for Android'
        url 'https://github.com/mingyuan-xia/PATDroid'

        scm {
          connection 'scm:git:git@github.com:mingyuan-xia/PATDroid.git'
          developerConnection 'scm:git:git@github.com:mingyuan-xia/PATDroid.git'
          url 'https://github.com/mingyuan-xia/PATDroid'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'mingyuan-xia'
            name 'Mingyuan Xia'
            email 'mxia@mxia.me'
          }

          developer {
            id 'iceb0y'
            name 'Lu Gong'
            email 'me@iceboy.org'
          }
        }
      }
    }
  }
}
